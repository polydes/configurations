plugins {
	id 'eclipse'
	id 'java-library'
}

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(21))
    }
}

tasks.withType(JavaCompile).each {
	it.options.compilerArgs.add('--enable-preview')
}

//building an artifact using a local-only build artifact of Stencyl
var localBuild = project.hasProperty("local_build")
//developing as a submodule of Stencyl
var localDev = project.hasProperty("local_dev")

group = 'com.polydes'
version = rootProject.file('version.txt').text.trim()

sourceSets {
	main {
	    java {
            srcDir 'src'
        }
    }
}

processResources {
    from('res') {
    	into 'res/com/polydes/configurations'
    }
    from(rootProject.file('icon.png')) {
        into 'com/polydes/configurations/res'
    }
}

repositories {
	if(localBuild) {
		mavenLocal()
	}
	mavenCentral()
	maven {
		url "https://www.stencyl.com/dl/maven2/releases"
	}
	maven {
		url "https://www.stencyl.com/dl/maven2/snapshots"
	}
}

dependencies {
	//Stencyl and its dependencies
    implementation enforcedPlatform('com.stencyl:stencyl-platform:4.1.0-SNAPSHOT')
	if(localDev) {
		compileOnly project(":")
		compileOnly project(":stencyl-v5-core")
		compileOnly project(":stencyl-v5-toolset")
	} else {
		compileOnly group: 'com.stencyl', name: 'stencyl', version: '4.1.0-SNAPSHOT'
	}
	compileOnly group: 'com.stencyl',        name: 'jide-oss'
    compileOnly group: 'org.apache.commons', name: 'commons-lang3'
	compileOnly group: 'log4j',              name: 'log4j'
    compileOnly group: 'commons-io',         name: 'commons-io'
}

jar {
	from {
		configurations.runtimeClasspath.collect { zipTree(it) }
	}
    manifest {
		attributes(
			"Extension-ID": "com.polydes.configurations",
			"Extension-Main-Class": "com.polydes.configurations.ConfigurationsExtension",
			"Extension-Version": version,
			"Extension-Icon": "com/polydes/configurations/res/icon.png",
			"Extension-Dependencies": "",
			"Extension-Type": "game",
			"Extension-Name": "Configurations",
			"Extension-Description": "Allows game to be configured for conditional compilation.",
			"Extension-Author": "Justin Espedal",
			"Extension-Website": "http://www.polydes.com/repo/toolset/com.polydes.configurations",
			"Extension-Repository": "http://www.polydes.com/repo",
			"Extension-Internal-Version": "1"
		)
	}
}